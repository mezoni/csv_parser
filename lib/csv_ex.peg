%%

  final String _separator;

  final int _separatorChar;

  CsvExParser({
    String separator = ',',
  })  : _separator = separator.isEmpty
            ? throw ArgumentError('Must not be empty', 'separator')
            : separator,
        _separatorChar = separator.runes.length != 1
            ? throw ArgumentError.value(separator, 'separator',
                'The length of the separator must be one character')
            : separator.runes.first;

%%

@event
Start = v:Rows Eof ;

@inline
Chars = ($[^"]+ / '""' <String>{ $$ = '"'; })* ;

@inline
CloseQuote = '"' Spaces ;

@inline
Eof = !. ;

Eol = [\n\r] / '\r\n';

@event
Field = String / Text ;

@inline
OpenQuote = Spaces '"' ;

@inline
RowEnding = Eol !Eof ;

@event
Row = @sepBy(Field, Sep) ;

Rows = v:@sepBy(Row, RowEnding) Eol? ;

@inline
Sep = @matchString({ _separator }) ;

Spaces = [ \t]* ;

String
String = OpenQuote v:Chars CloseQuote { $$ = v.join(); } ;

Text = $TextChar* ;

@inline
TextChar = @verify([^"\n\r], { if ($$ == _separatorChar) { error = ErrorUnexpectedCharacter(); } }) ;